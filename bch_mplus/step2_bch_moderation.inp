TITLE:
Step2_bch_automation
DATA:
FILE = "/Users/agarber/github/BCH-MplusAuto/bch_mplus/step2_bch_moderation.dat";
 
VARIABLE:
NAMES = REPORT_D REPORT_R REPORT_S COUNSELO PSYCH_FT LAW_FTE LUNCH_PR READ_TES
     MATH_TES BCHW1 BCHW2 BCHW3 CPROB1 CPROB2 CPROB3 N; 
 MISSING=.;
 usevar = BCHW1-BCHW3 read_tes math_tes lunch_pr;
  missing are all (999); 
  classes = c(3); 
  training = BCHW1-BCHW3(bch); 
DEFINE:
center lunch_pr (grandmean);
ANALYSIS:
estimator = mlr; 
  type = mixture; 
  starts = 0;
MODEL:
!!! DISTAL OUTCOMES = read_tes math_tes !!!
  !!! COVARIATE = lunch_pr !!!
  
  %OVERALL%
  
  C on lunch_pr;
 
  read_tes on lunch_pr;
  read_tes;
  
  math_tes on lunch_pr;
  math_tes;

  %C#1%

  [read_tes](m01);               !!! estimate conditional intercept mean !!!
  read_tes;                      !!! estimate conditional intercept variance !!!
  read_tes on lunch_pr (s01);    !!! estimate conditional regression !!!

  [math_tes] (m1);
  math_tes;
  math_tes on lunch_pr (s1);

  %C#2%

  [read_tes](m02);
  read_tes;
  read_tes on lunch_pr (s02);

  [math_tes] (m2);
  math_tes;
  math_tes on lunch_pr (s2);
  
  %C#3%

  [read_tes](m03);
  read_tes;
  read_tes on lunch_pr (s03);

  [math_tes] (m3);
  math_tes;
  math_tes on lunch_pr (s3);  
MODEL CONSTRAINT:
New (rdiff12 rdiff13
  rdiff23 rslope12 rslope13
  rslope23 mdiff12 mdiff13
  mdiff23 mslope12 mslope13
  mslope23);

  rdiff12 = m1-m2;   mdiff12 = m01-m02;
  rdiff13 = m1-m3;   mdiff13 = m01-m03;
  rdiff23 = m2-m3;   mdiff23 = m02-m03;
  rslope12 = s1-s2;  mslope12 = s01-s02;
  rslope13 = s1-s3;  mslope13 = s01-s03;
  rslope23 = s2-s3;  mslope23 = s02-s03;
MODEL TEST:
!m01=m02;    !!! Distal outcome omnibus Wald test for `read_tes` !!!
  !m02=m03;

  !s01=s02;   !!! Slope difference omnibus Wald test for `read_tes on lunch_pr` !!!
  !s02=s03;
  
  m1=m2;       !!! Distal outcome omnibus Wald test for `math_tes` !!!
  m2=m3;       

  !s1=s2;      !!! Slope difference omnibus Wald test `math_tes on lunch_pr` !!!
  !s2=s3;  
 
 
